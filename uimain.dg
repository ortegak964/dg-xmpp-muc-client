import '/hashlib/md5'
import '/time/strftime'
import '/collections/defaultdict'

import '/gi/repository/Gdk'
import '/gi/repository/Gtk'
import '/gi/repository/Pango'
import '/gi/repository/GObject'

import 'gtktools/delegate'
import 'xmpptools/Client'


GObject.type_register     (LoginWidgetT = inherit Gtk.Grid ())
GObject.signal_new 'login' LoginWidgetT GObject.SIGNAL_RUN_LAST GObject.TYPE_NONE $ tuple' object


LoginWidget = (username password rooms) -> wnd where
  wnd = LoginWidgetT!
  wnd.add $ Gtk.Frame.with loginform label: 'Login' shadow_type: Gtk.ShadowType.ETCHED_IN where
    jidf = Gtk.Entry text: username placeholder_text: 'Jabber ID'
    pwdf = Gtk.Entry text: password placeholder_text: 'Password' visibility: False
    ok   = Gtk.Button 'Login'
    ok.connect 'clicked' _ ->
       wnd.emit 'login' $ Client jidf.props.text pwdf.props.text rooms
    loginform = Gtk.Grid.with jidf pwdf ok orientation: Gtk.Orientation.VERTICAL
  wnd.show_all!


MUCWidget = (self room token) -> Gtk.Paned.with
  orientation: Gtk.Orientation.VERTICAL

  Gtk.Paned.with
    hexpand: True
    vexpand: True

    Gtk.Frame.scrollable view hexpand: True vexpand: True auto_rewind: True where
      # colorify :: str -> str
      #
      # Generate a preudorandom color given a seed string.
      # Luminance of that color is capped at 0.5.
      #
      colorify = x ->
        r, g, b = take 3 $ (md5 $ x.encode 'utf-8').digest!
        m = min 0 (127 - 0.299 * r - 0.587 * g - 0.114 * b)
        '#{:0>2x}{:0>2x}{:0>2x}'.format *: (map (bind max 0 <- round m +) (r, g, b))

      view = Gtk.TextView.linkifyable
        editable:       False
        cursor_visible: False
        wrap_mode:      Gtk.WrapMode.WORD

      buffer = view.props.buffer
      time      = buffer.tag foreground: '#777777'
      highlight = buffer.tag foreground: '#990011'
      text      = buffer.tag foreground: '#333333'
      system    = buffer.tag foreground: '#dd4400' style:  Pango.Style.ITALIC
      ntag = n -> buffer.tag ('n#' + n) foreground: (colorify n) weight: Pango.Weight.BOLD
      mtag = n -> buffer.tag ('m#' + n) foreground: (colorify n) style:  Pango.Style.ITALIC

      buffer.emotes = dict'
        ':)',  'face-smile'
        ':-)', 'face-smile'
        ':D',  'face-laugh'
        ':-D', 'face-laugh'
        ':(',  'face-sad'
        ':-(', 'face-sad'
        ":'(", 'face-crying'
        ':|',  'face-plain'
        ':-|', 'face-plain'
        ':<',  'face-worried'

      joined = x ->
        buffer.append (strftime '\n%H:%M:%S +') time
        buffer.append x.nick $ ntag x.nick

      left = x ->
        buffer.append (strftime '\n%H:%M:%S âˆ’') time
        buffer.append x.nick $ ntag x.nick

      message = x ->
        x.time = strftime '\n%H:%M:%S '

        switch
          x.nick == '' =  # Server-issued message.
            buffer.append  x.time time
            buffer.linkify x.body system

          x.body.startswith '/me ' =  # Self-referencing message.
            buffer.append   x.time                    time
            buffer.append  (x.nick + ' ')           $ mtag x.nick
            buffer.linkify (x.body !! slice 4 None) $ mtag x.nick

          self.muc.ourNicks !! room in x.body =  # Highlighted message.
            buffer.append  x.time   highlight
            buffer.append  x.nick $ ntag x.nick
            buffer.append  ': '     time
            buffer.linkify x.body   text

          True =  # Boring stuff you shouldn't read.
            buffer.append  x.time   time
            buffer.append  x.nick $ ntag x.nick
            buffer.append  ': '     time
            buffer.linkify x.body   text

      subject = x ->
        buffer.append (strftime '\n%H:%M:%S ') time
        buffer.append  x.nick system
        buffer.append  ' has set the subject to: ' system
        buffer.linkify x.subject system

      self.associate token ('muc::{}::subject'.format     room) $ delegate subject
      self.associate token ('muc::{}::message'.format     room) $ delegate message
      self.associate token ('muc::{}::got_online'.format  room) $ delegate joined
      self.associate token ('muc::{}::got_offline'.format room) $ delegate left

    Gtk.Frame.scrollable roster vexpand: True where

      roster = Gtk.TreeView model where
        # status :: str -> str
        #
        # Get an icon name given a presence type.
        #
        status = x -> (dict
          dnd:  'empathy-busy'
          away: 'empathy-away'
          xa:   'empathy-extended-away'
        ).get x 'empathy-available'

        model = Gtk.ListStore str str
        model.set_sort_column_id 0 Gtk.SortType.ASCENDING

        self.associate token ('muc::{}::presence'.format room) $ delegate p ->
          # 1. Remove the old entry.
          for (model.find p.nick 0) (model !!~)
          # 2. Add the same entry.
          #    (Easier than checking if there's already one.)
          model.append (p.nick, status p.type) if p.type != 'unavailable'
          # 3. ???????
          # 4. False. (Wait, no, `delegate` will return `False` anyway.)

      roster.set_headers_visible False
      roster.append_column $ Gtk.TreeViewColumn '' Gtk.CellRendererPixbuf! icon_name: 1
      roster.append_column $ Gtk.TreeViewColumn '' Gtk.CellRendererText!   text: 0

  Gtk.Frame.scrollable entry hexpand: True where
    # send :: TextBuffer -> bool
    #
    # Send a message to the active room, return True.
    #
    send = b -> b.set_text '' where self.send_message room b.props.text mtype: 'groupchat'

    entry = Gtk.TextView editable: True wrap_mode: Gtk.WrapMode.WORD
    entry.connect 'key-press-event' $ (w ev) -> switch
      # `Return` sends the message.
      # `Tab` autocompletes a nickname at the start of the message.
      # Using any of these with `Shift` reverts them to their original action.
      ev.state & Gdk.ModifierType.SHIFT_MASK = False
      ev.keyval == Gdk.KEY_Return = True where send w.props.buffer
      ev.keyval == Gdk.KEY_Tab    = True where w.props.buffer.autocomplete
        map (+ ', ') $ self.muc.rooms !! room


ErrorWidget = (self p) -> Gtk.Grid.padded
  orientation: Gtk.Orientation.VERTICAL

  Gtk.Label hexpand: True $ switch
    p.error == 'conflict'              = 'Nickname {.nick} already taken.'.format p
    p.error == 'forbidden'             = 'Banned.'
    p.error == 'not-allowed'           = "Couldn't create a new room."
    p.error == 'item-not-found'        = 'This room is locked.'
    p.error == 'not-authorized'        = 'Password required.'
    p.error == 'service-unavailable'   = 'This room is overcrowded.'
    p.error == 'registration-required' = 'Not a member of this room.'
    True = 'Unknown error: {.error}.'.format p

  switch
    p.error == 'conflict' = Gtk.Grid.padded entry retry vexpand: False where
      entry = Gtk.Entry text: (p.nick + "'")
      retry = Gtk.Button 'Try again'

      retry_f = _ ->
        entry.set_sensitive False
        retry.set_sensitive False
        self.muc.joinMUC p.room entry.props.text maxhistory: '20'

      entry.connect 'activate' retry_f
      retry.connect 'clicked'  retry_f

    p.error == 'not-authorized' = Gtk.Grid.padded entry retry vexpand: False where
      entry = Gtk.Entry visibility: False placeholder_text: 'Know one?'
      retry = Gtk.Button 'Try again'

      retry_f = _ ->
        entry.set_sensitive False
        retry.set_sensitive False
        self.muc.joinMUC p.room p.nick password: entry.props.text maxhistory: '20'

      entry.connect 'activate' retry_f
      retry.connect 'clicked'  retry_f

    True = Gtk.Grid.padded retry vexpand: False where
      retry = Gtk.Button 'Try again'
      retry.connect 'clicked' _ ->
        retry.set_sensitive False
        self.muc.joinMUC p.room p.nick maxhistory: '20'


MainWindow = (username password rooms) -> wnd where
  self = None

  wnd = Gtk.Window.with tabs where
    tabs = Gtk.Notebook show_border: False tab_pos: Gtk.PositionType.BOTTOM

    toks = defaultdict list
    nums = dict!
    wgts = dict!

    login = LoginWidget username password rooms
    login.connect 'login' (_ client) ->
      self = client
      self.add_event_handler 'join' $ delegate $ (room, nick) -> room in nums or
        wgts !! room = w = MUCWidget self room $ toks !! room
        nums !! room = n = tabs.append_page w grid where
          close = Gtk.Image.new_from_stock Gtk.STOCK_CLOSE Gtk.IconSize.MENU

          button = Gtk.Button.with close focus_on_click: False relief: Gtk.ReliefStyle.NONE
          button.connect 'clicked' $ _ -> self.event 'leave' room

          grid = Gtk.Grid.with (Gtk.Label room) button
          grid.show_all!

        self.muc.joinMUC room nick maxhistory: '20'

      self.add_event_handler 'leave' $ delegate $ room -> room in nums and
        self.muc.leaveMUC room (self.muc.ourNicks !! room)
        self.dissociate  $ toks.pop room
        tabs.remove_page $ nums.pop room
        wgts.pop room

      self.add_event_handler 'groupchat_presence' $ delegate $ p -> switch
        self.muc.ourNicks !! p.room != p.nick = None
        p.type == 'error' = tabs.replace_page (nums !! p.room) $ ErrorWidget self p
        p.type != 'error' = tabs.replace_page (nums !! p.room) $ wgts !! p.room

      self.process! if self.connect!

    tabs.append_page login (Gtk.Label '+')

  wnd.connect 'delete-event' Gtk.main_quit
  wnd.connect 'delete-event' (_ _) -> (self.disconnect! if self)
  wnd.show_all!
