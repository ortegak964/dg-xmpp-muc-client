import '/gi/repository/Gtk'
import '/sleekxmpp/Message'

import '../gtktools/delegate'

import 'muc/MUCWidget'
import 'chat/ChatWidget'
import 'login/LoginWidget'


Signal = inherit list $
  emit    = (self *: a **: k) -> exhaust $ map f -> (f *: a **: k) self
  connect = list.append


MainWindow = (username password rooms) -> wnd where
  self = None

  wnd = Gtk.Window.with tabs where
    signals = dict!

    tabs = Gtk.Notebook show_border: False tab_pos: Gtk.PositionType.BOTTOM
    tabs.set_scrollable True

    add_page = (Type target onquit source) ->
      signals !! (Type, target) = dict
        onmessage:  (onmsg = Signal!)
        onpresence: (onprs = Signal!)

      w = Type self target onmsg onprs
      w.show_all!

      tabs.set_current_page $ tabs.append_page w grid where
        close  = Gtk.Image.new_from_stock Gtk.STOCK_CLOSE Gtk.IconSize.MENU
        button = Gtk.Button.with close focus_on_click: False relief: Gtk.ReliefStyle.NONE
        button.connect 'clicked' _ ->
          onquit target
          tabs.remove_page $ tabs.page_num w
          signals.pop (Type, target)

        grid = Gtk.Grid.with (Gtk.Label $ str target) button
        grid.show_all!

      onmsg.emit source if source :: Message else onprs.emit source

    leave_muc = room ->
      self.muc.leaveMUC room (self.muc.ourNicks !! room)

    login = LoginWidget username password rooms client ->
      self = client
      self.add_event_handler 'message' $ delegate $ m -> switch
        (ChatWidget, m.from) in signals = (signals !! (ChatWidget, m.from) !! 'onmessage').emit m
        (MUCWidget,  m.room) in signals = (signals !! (MUCWidget,  m.room) !! 'onmessage').emit m
        m.type == 'groupchat' = add_page MUCWidget  m.room leave_muc m
        m.type == 'chat'      = add_page ChatWidget m.from _ -> _ m
        m.type == 'normal'    = add_page ChatWidget m.from _ -> _ m

      self.add_event_handler 'presence' $ delegate $ p -> switch
        (ChatWidget, p.from) in signals = (signals !! (ChatWidget, p.from) !! 'onpresence').emit p
        (MUCWidget,  p.room) in signals = (signals !! (MUCWidget,  p.room) !! 'onpresence').emit p
        p.room in self.muc.ourNicks = add_page MUCWidget p.room leave_muc p

      self.process! if self.connect!

    tabs.append_page login (Gtk.Label '+')
    login.show_all!

  wnd.connect 'delete-event' Gtk.main_quit
  wnd.connect 'delete-event' (_ _) -> (self.disconnect! if self)
  wnd.show_all!
